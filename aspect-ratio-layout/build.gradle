plugins {
    id 'com.jfrog.bintray' version '1.2'
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'
apply plugin: 'signing'

group           = 'me.mattak'
version         = '0.1.0'
def archiveName = 'aspect-ratio-layout'
def userId      = 'mattak'
def userName    = 'Takuma Maruyama'
def userEmail   = 'mattak.me@gmail.com'
def gitUrl      = 'https://github.com/mattak/AspectRatioLayout.git'
def siteUrl     = 'https://github.com/mattak/AspectRatioLayout'
def issueUrl    = 'https://github.com/mattak/AspectRatioLayout/issues'
def projectName = 'Aspect Ratio Layout'
def projectDescription = 'Aspect ratio based layout for android'
def scmUrl      = 'scm:git@github.com:mattak/AspectRatioLayout.git'
def licenseName = 'The Apache License, Version 2.0'
def licenseUrl  = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
def isReleaseVersion = !version.endsWith('SNAPSHOT')

android {
    compileSdkVersion 22
    buildToolsVersion "23.0.0"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 22
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    jacoco {
        version = '0.7.2.201409121644'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    packagingOptions {
        exclude 'LICENSE'
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    configurations = ['archives']

    pkg {
        repo            = 'maven'
        name            = archiveName
        vcsUrl          = gitUrl
        websiteUrl      = siteUrl
        issueTrackerUrl = issueUrl
        licenses        = ['Apache-2.0']
        labels          = ['android']
        publicDownloadNumbers = true
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name      archiveName
                url       siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url  'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                // developers.developer {
                //     id    userId
                //     name  userName
                //     email userEmail
                // }
                scm {
                    connection          gitUrl
                    developerConnection gitUrl
                    url                 siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

task findConvensions << {
    println project.getConvention()
}

signing {
    required { gradle.taskGraph.hasTask('uploadArchives') }
    sign configurations.archives
}

uploadArchives {
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        def sonatypeRepo = isReleaseVersion ?
            'https://oss.sonatype.org/service/local/staging/deploy/maven2/' :
            'https://oss.sonatype.org/content/repositories/snapshots/'

        repository(url: sonatypeRepo) {
            authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        pom.artifactId = archiveName
        pom.project {
            name projectName
            packaging 'aar'
            description projectDescription
            url siteUrl

            scm {
                url scmUrl
                connection scmUrl
                developerConnection scmUrl
            }

            licenses {
                license {
                    name licenseName
                    url  licenseUrl
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id    userId
                    name  userName
                    email userEmail
                }
            }
        }
    }
}
